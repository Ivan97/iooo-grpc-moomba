apply plugin: 'com.google.protobuf'
apply plugin: "com.google.osdetector"

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

def tcnative_classifier = osdetector.classifier;
// Fedora variants use a different soname for OpenSSL than other linux distributions
// (see http://netty.io/wiki/forked-tomcat-native.html).
if (osdetector.os == "linux" && osdetector.release.isLike("fedora")) {
    tcnative_classifier += "-fedora";
}

dependencies {
    implementation "io.grpc:grpc-all:${grpcVersion}"
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation 'io.netty:netty-tcnative:2.0.20.Final:' + tcnative_classifier
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    testImplementation "tech.iooo.boot:iooo-boot-test-bundle:${IOOO_BOOT_VERSION}"
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
}
